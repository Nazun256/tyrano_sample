*start

[title name="☆ときめきくゎんたむ☆"]
[hidemenubutton]
[wait time=300]
[freeimage layer="base"]
[position left="10" top="452" width="1260" height="266" frame="window0/_frame5.png"]
[position margint="60" marginl="50" marginr="25" marginb="10"]
[loadcss file="data/others/font/font.css"]
[deffont size="40" face="ロゴたいぷゴシック,メイリオ,sans-serif" color="0xffffff" edge="0x000000"]
[resetfont]
[chara_new  name="ryouko" storage="chara/ryouko/ryouko1.png" ]
[chara_new name="ryouko2" storage="chara/ryouko/ryouko2.png" ]
[chara_new name="ryouko3" storage="chara/ryouko/ryouko3.png" ]
[chara_new name="ryouko4" storage="chara/ryouko/ryouko4.png" ]
[iscript]
f.favo_ryouko = 0
[endscript]
[chara_hide name=ryouko3]
[chara_hide name=ryouko4]

今日から新学期。あなたはこれから学校に通います。[p]
「勉強とか大変そうだけどこれから頑張ろう！」[p]
[bg storage=room.jpg time=500]
「そういえば、入学式のときにかわいい子がいたな。おんなじクラスだといいな！」[p]
...がやがや[p]
席に着く。なんだかまわりが騒がしい。[p]
「お、おい。あれって...」「なあなあ見ろよ！」[p]
まわりの席の男子たちが視線を向ける先、そこにはこの世のものとは思えないような風貌の美しい少女がいた。[p]
[chara_show name=ryouko zindex=10 top=200]
「入学式で見たあの子だ！」[r]僕は心の中で叫んだ。[p]
座標変換に共変性をもつその姿は、天衣無縫のさまを思わせる。頭が良いだけでなく、運動もよく（記述）できるのであろう。[p]
あなたは彼女を遠目に眺めていたが、しばらくして彼女はおもむろにあなたのほうに近づいてくる。すると、不意に目が合う。[p]
「私の席、そこみたいね。失礼するわ。」[p]
！！！[r]なんと、その子はあなたの隣の席だった！最初の印象は大事だ。何か話さなければ...[p]
あなたは突然のことで頭を回らない中で、精一杯話題を考えた。そうだな...[p]
[glink color=blue size=30  x=300 width=600 y=100 target=*tag_11 text=「これからよろしく」]
[glink color=blue size=30  x=300 width=600 y=200 target=*tag_12 text=「シュレディンガー派なんですね」]
[glink color=blue size=30  x=300 width=600 y=300 target=*tag_13 text=(寝たふり)]
[s]

*tag_11
「隣の席ですね。これからよろしくお願いします。」あなたは勇気を振り絞って挨拶をした。[p]
「こちらこそよろしくね。」[p]
よかった、無視されなかったと胸をなでおろす。[p]
「名前はなんて言うんですか？」[p]
「私、量子って言うの。あとタメ口でいいわよ。」[p]
...[p]
その後も無難な会話が続いた。なんとかうまく取り繕えたけど、ちょっと硬かったかなあ...。[p]
（好感度+1）[p]
[iscript]
f.favo_ryouko +=1
[endscript]
[jump target=*tag_20]

*tag_12
「シュレディンガー描像のほうが好きなの？」[p]
「あら、私のことどこかで知ってくれてるのかしら？今はシュレディンガー描像だけれど、スピンするときなんかはハイゼンベルク描像のことが多いわね。どちらも数学的には等価なのよ」[p]
「へえ。このあたりだとユークリッド空間に住んでるの？」[p]
「近いんだけど違うわ。ヒルベルト空間に住んでるわ。極限の存在が仮定されているから、微積が定義できるのよ。」[p]
「...そうそう、私は量子っていうの。よろしくね」[p]
話が弾んだ！良かったあ。[p]
(好感度+2)[p]
[iscript]
f.favo_ryouko +=2
[endscript]
[jump target=*tag_20]

*tag_13
とっさに寝たふりをした。机に突っ伏してやり過ごすことにした。突然過ぎてどうすればいいのかわからなかった。[p]
「あなた、さっき私と目があったじゃない。無視するなんてひどいんじゃないかしら？」[p]
...まずい。何とかしなければ[p]
「別に無視したわけじゃなくて... 君みたいな人が隣になるなんてびっくりで、つい。」[p]
「どういう意味かしら、それ。」[p]
...印象を悪くしてしまったようだ。[p]
...[p]
それから、チャイムが鳴り、クラスで自己紹介をすることになった。[p]
どうやら彼女の名前は量子ちゃんというらしい。[p]
(好感度+0)[p]
[jump target=*tag_20]

*tag_20
[chara_hide name=ryouko]
（中略）[p]
授業が始まった。[p]
今は化学の時間だ。何やら原子についての内容のようだ。先生が話をしている。[p]
「原子は、今では当たり前のものとして知られていますが、昔は観測できない、存在を直接確認できないということで幾度となく否定されてきました。」[p]
「科学の大原則として、観測したものを原点として、それらをもとに現象を説明できるように理論を構築していきます。」[p]
「観測できないものはすべて仮定ですから、反論の対象になってしまうわけです。」[p]
「ブラウン運動という現象がありました。水の中に花粉のような小さな粒を入れると小刻みに震えるという現象です。」[p]
「現象自体は古くから知られていましたがなぜかはうまく説明できませんでした。」[p]
「アインシュタインはこの現象を水分子のランダムな衝突が原因として説明し、なんとそこからすでに知られていたアボガドロ数を算出しました。」[p]
「実際に原子が観測されるのはもう少し後ですが、原子の存在はこうした実験や理論によって少しずつ認められていきました。」[p]
「...皆さんにはこれから、原子について調べて発表してもらいます。隣の席の人とペアになって来週以降の授業で発表してもらいます。」[p]

こうして隣の席同士の僕と量子ちゃんは調べもの課題を一緒にやることになった。[p]
[chara_show name=ryouko zindex=10 top=200]
「原子についてなんでも調べろだなんてずいぶんざっくりしてるわね。まずはテーマを決めようかしら。」[p]
量子ちゃんはこういうのに詳しそうだ。見た目に違わず頭がいいようだ。これは仲を深めるチャンスだ！[p]
「あなたはどんなテーマがやりたいのかしら？」[p]
[glink color=blue size=30  x=300 width=600 y=100 target=*tag_21 text=パウリの排他律について調べる]
[glink color=blue size=30  x=300 width=600 y=200 target=*tag_22 text=発表を担当する]
[glink color=blue size=30  x=300 width=600 y=300 target=*tag_23 text=分子の結合について調べる]
[s]

*tag_21
「教科書の脚注に書いてあるこれ、ちょっとよくわからないけど興味あるな。こういうのどうかな。」[p]
「パウリの排他律...電子はフェルミ粒子だから、1つの軌道に入る電子は1つしかなくてエネルギー的に安定な軌道から詰まっていくことを表す規則ね。」[p]
「いいわ、それにしましょう。放課後空いてるかしら？一緒に調べに行きましょう。」[p]
[chara_hide name=ryouko]
それから図書館に行き、量子ちゃんと勉強することになった。[p]
（中略）[p]
発表の日になった。[p]
「原子核の周りには一番内側から順にK核、L核、M核...があって、内側から電子が入っていきます。」[p]
[chara_show name=ryouko zindex=10 top=200]
「核の中にはさらにs軌道、p軌道、d軌道、f軌道というのがあって、さらに軌道の中にもスピン自由度があって上向きスピンと下向きスピンの電子が...」[p]
（中略）[p]
僕は前半部分、量子ちゃんは後半部分を担当した。量子ちゃんの言ってることは正直全然わからなかったけど、僕なりに頑張ったし先生からもいい評価をもらった。[p]
量子ちゃんにはすごく助けてもらって、仲も深められたと思う。[p]
「カリウムは3d軌道じゃなくて4s軌道に先に電子が入るから、厳密にはM核が埋まりきる前にN核にも電子が入るけど...全体的にいい発表だったんじゃないかしら？」[p]
「特に電子が離散的に存在するところの説明が良かったわ。何にしろ...あなたと組めてよかったわ。」[p]
(好感度+2)[p]
[iscript]
f.favo_ryouko +=2
[endscript]
[jump target=*tag_30]

*tag_22
「量子ちゃんのほうが詳しいと思うから、内容を任せるよ。僕は発表を担当するね。」[p]
僕よりも量子ちゃんのほうが頭よさそうだから、内容は任せたほうが賢明に違いない！[p]
「わかったわ。」[p]
[chara_hide name=ryouko]
（中略）[p]
発表の日になった。[p]
「ボーアモデルの導出...？なに、この式。実際には電子は確率密度関数であらわされる...？ちょっと何言ってるかわからない」[p]
量子ちゃんに渡された発表資料は最初から最後まで難しい内容だった。一応頑張って理解しようとしたけど無理があるよねこれ。[p]
「ええと、電子は原子核の周りを円運動してて...」[p]
（中略）[p]
結局、発表は失敗に終わってしまった。[p]
[chara_show name=ryouko zindex=10 top=200]
「円運動だなんて、全く内容理解できてないじゃない。発表を担当するっていうから任せたけれど、私が発表したほうが良かったわ。」[p]
(好感度+0)[p]
[jump target=*tag_30]

*tag_23
「教科書の先の範囲を発表するのはどうかな。『原子と原子の結合はおおむね4種類』だって。」[p]
どのみちテストでやる範囲だから無駄がないに違いない！僕は教科書の数ページ先を指さした。[p]
「結合の種類についての範囲ね。いいんじゃないかしら」[p]
[chara_hide name=ryouko]
（中略）[p]
発表の日になった。[p]
「結合には金属結合、分子結合、イオン結合などがあって...。」[p]
内容はごく平易なものだ。無難に発表を終えた。[p]
[chara_show name=ryouko zindex=10 top=200]
「発表、まあまあだったんじゃないかしら。」[p]
特に大きなミスなどはなかったが、量子ちゃんは物足りなさそうにしている。[p]
「せっかくなら優秀賞が取りたかったわね。」[p]
(好感度+1)[p]
[iscript]
f.favo_ryouko +=1
[endscript]
[jump target=*tag_30]

*tag_30
[chara_hide name=ryouko]
（中略）[p]
[bg storage="fireworks.jpeg"]
それからしばらくして、量子ちゃんとデートに行くことになった。夏祭りで花火大会があるようだ。[p]
[chara_show name=ryouko2 zindex=10 top=200 width=800]
「今日は誘ってくれてありがとう！」[p]
「今日はブラケット記法だね。」[p]
いつもと違ってブラケットな彼女は、夜の明りに照らされてひときわ美しい。[p]
「気づいてくれてありがとう！今日はハイゼンベルク描像にしてみたの。」[p]
「それにしても、周りのエネルギーが高くて摂動しちゃうわね///」[p]
[chara_hide name=ryouko2]
（中略）[p]
「花火、きれいだね。」[p]
[chara_show name=ryouko2 zindex=10 top=200 width=800]
「そうね、あれはストロンチウムかしら。電子は軌道ごとに離散的なエネルギーを持っているから、励起した電子が元の軌道に戻るとき、差分のエネルギーをもつ単一スペクトルの光が出るの。」[p]
[chara_hide name=ryouko2]
（中略）[p]
[bg storage="stars.jpeg"]
「星、きれいだね。」[p]
[chara_show name=ryouko2 zindex=10 top=200 width=800]
「そうね、光は巨視的には波だけど、粒でもあるから目に見えているのよ。」[p]
「波は距離の2乗に反比例して減衰するわ。距離が1のとき1としたら、距離が2で0.25、距離が3で0.11みたいな感じね。」[p]
「だから本来、あまりに遠くにある星の光は到底私たちの目には映らない。エネルギーが小さくなりすぎて、目が認識できなくなってしまう。」[p]
「でもね、光は粒としての性質も持っているから、hνというエネルギーを必ず持っていて、距離の減衰は密度の減少として理解される。」[p]
「ヒトの目はこのE = hνの光を光と認識できるから、こうして満天の星空を楽しむことができる。」[p]
「...私も実は波であり粒でもあるの。こんな二面性のある女、嫌かしら。」[p]
[glink color=blue size=30  x=300 width=600 y=100 target=*tag_31 text=「星は天球上に張り付いているんだよ」]
[glink color=blue size=30  x=300 width=600 y=200 target=*tag_32 text=「波は波だ、君は君だ」]
[glink color=blue size=30  x=300 width=600 y=300 target=*tag_33 text=「波でも粒でもどっちでもいいと思う、ありのままの君を受け入れよう」]
[s]

*tag_31
「星は天球上に張り付いていて、天球が回っているんだ。波か粒かなんて関係ないよ。」[p]
「あなたは物事を表面でしか捉えないのね。」[p]
「えっ」[p]
「なんでもないわ。そろそろ時間だし帰りましょう。」[p]
(好感度+0)[p]
[jump target=*tag_40]

*tag_32
「波は波だ。君は君だよ。どっちが正解かなんて関係ないんだ。」[p]
「そう、ありがとね。でも、それじゃ困ることもあるのよ。」[p]
「そんな。僕は別に気にしない。別に、それで、いいじゃないか。」[p]
「そう言ってくれるだけでありがたいわ。」[p]
(好感度+1)[p]
[iscript]
f.favo_ryouko +=1
[endscript]
[jump target=*tag_40]

*tag_33
「二面性を持つことこそが本質だよ。波でも粒でもいい。両方とも君だ。」[p]
「本当に？こんな私でもいいのかしら？」[p]
「もちろんだよ。ありのままの君を受け入れるよ。」[p]
「そう言ってもらえると...本当に...うれしい！」[p]
(好感度+2)[p]
[iscript]
f.favo_ryouko +=2
[endscript]
[jump target=*tag_40]

*tag_40
[chara_hide name=ryouko2]
（中略）[p]
[bg storage="restaurant.jpg"]
それからまたしばらく経った。今日は量子ちゃんをディナーに誘った。[p]
「そろそろお付き合いしたい...今日こそ決めるぞ！緊張するなあ。」[p]
待ち合わせ。某イタリアンレストランに誘った。[p]
ところが、なんということか。待ち合わせ時刻に遅れてしまった。[p]
「ごめん、待たせたよね。」[p]
[chara_show name=ryouko3 zindex=10 top=200 width=1280]
「気にしないわ。入りましょう。」[p]
それから僕たちはお店に入ると、ナップザック問題を解いて円あたりのカロリーが高くなるように、大ライス、ライス、アーリオオーリオを頼んだ。[p]
「量子ちゃん、実は今日、お話があるんだ。」[p]
[glink color=blue size=30  x=200 width=800 y=150 target=*tag_41 text=すべては決定論的に決まっている。告白が成功するかもすでに決まっている。堂々と気持ちを伝えよう。]
[glink color=blue size=30  x=200 width=800 y=250 target=*tag_42 text=告白はやってみないとわからない。まだ成功と失敗の重ね合わせ状態だ。誠実に気持ちを伝えよう。]
[s]

*tag_41
「僕と君は運命によって結ばれているんだ。今までも、これからも、僕たちは一緒にいるべきだ。量子ちゃん、付き合ってほしい。」[p]
「すべてが運命によって決まっているだなんて、そんなことはないわ。」[p]
「局所実在性はベルの不等式の破れから否定された。実在性は幻想なのよ。」[p]
(好感度+0)[p]
[jump target=*tag_50]

*tag_42
「今日は遅刻しちゃって本当にごめん！僕は量子ちゃんには及ばないかもしれないけど、それでも量子ちゃんと一緒にいたいんだ。」[p]
「でも私は実在しないの。あなたが見ているのは全部虚像かもしれない。確率的に、私は存在するの。それでもいいの？」[p]
「別にそんなの関係ないよ！確率的だとしても、僕は君といたいんだ。」[p]
(好感度+2)[p]
[iscript]
f.favo_ryouko +=2
[endscript]
[jump target=*tag_50]

*tag_50
[jump target=*tag_51 cond="f.favo_ryouko >=  7"]
[jump target=*tag_52 cond="f.favo_ryouko >=  4"]
[jump target=*tag_53]

*tag_51 
...[p]
「わかったわ。じゃあ付き合いましょう。でも、私がいるとあなたは迷惑かもしれないわ。」[p]
「今日あなたが遅れてきたのは、相対論君、私が時間に依存してるからよね。」[p]
「量子ちゃん...！そんなの僕たちなら乗り越えられるよ！」[p]
「相対論君...」[p]
おや、量子ちゃんの様子が...！[p]
[chara_hide name=ryouko3]
[chara_show name=ryouko4 zindex=40 top=200]
「ありがとう相対論君。確かに私たちなら乗り越えられる。」[p]
相対論君の告白は無事、成功したのだった。二人は後に、相対論的量子力学と呼ばれるのであった。めでたしめでたし。[p]
【GOOD END】THANK YOU FOR PLAYING!![p]
[glink color=blue size=30  x=300 width=600 y=150 target=*start text=スタートに戻る]
[s]

*tag_52
...[p]
「わかったわ。でもまだ付き合うべきじゃないと思うの。」[p]
「そんな...」[p]
「別にあなたのことが嫌いなわけじゃないの。お友達でいましょう。」[p]
【BAD END1】THANK YOU FOR PLAYING!![p]
（Tips）好感度7-8：GOOD END、好感度4-6：BAD END1、好感度0-3：BAD END2[p]
[glink color=blue size=30  x=300 width=600 y=150 target=*start text=スタートに戻る]
[s]

*tag_53
...[p]
「やっぱり私たちは一緒にいるべきではないわ。」[p]
「あなたには古典ちゃんのほうがいいんじゃないかしら。」[p]
「そんな...」[p]
「今日のことはなかったことにしましょう。さようなら。」[p]
【BAD END2】THANK YOU FOR PLAYING!![p]
（Tips）好感度7-8：GOOD END、好感度4-6：BAD END1、好感度0-3：BAD END2[p]
[glink color=blue size=30  x=300 width=600 y=150 target=*start text=スタートに戻る]
[s]
